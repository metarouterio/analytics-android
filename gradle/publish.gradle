apply plugin: 'maven-publish'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

group = GROUP
version = VERSION_NAME

publishing {
    publications {
        Production(MavenPublication) {
            artifact(sourcesJar)
            artifact("$buildDir/outputs/aar/analytics-release.aar")  {
                builtBy tasks.getByName("assembleRelease")
            }
            groupId GROUP
            artifactId 'MetarouterAnalytics'
            version this.version

            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')

                // Iterate over the implementation dependencies (we don't want the test ones), adding a <dependency> node for each
                configurations.implementation.allDependencies.each {
                    // Ensure dependencies such as fileTree are not included in the pom.
                    if (it.name != 'unspecified') {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

artifacts {
    archives sourcesJar
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")

    configurations = ['archives']
    pkg {
        repo = 'maven'
        name = 'io.metarouter.analytics.android'
        userOrg = 'metarouter'
        desc = 'The hassle-free way to add analytics to your Android app.'
        websiteUrl = 'https://metarouter.io'
        vcsUrl = 'https://github.com/super-collider/analytics-android.git'
        licenses = ['MIT']
        override = true
        publish = true
        publicDownloadNumbers = true
        version {
            name = this.version
            desc = "Version ${this.version}"
            released = new Date()
            vcsTag = this.version
        }
    }
}